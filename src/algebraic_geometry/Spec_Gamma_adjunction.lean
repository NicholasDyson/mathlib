/-
Copyright (c) 2021 Junyan Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Junyan Xu
-/
import algebraic_geometry.Spec
import algebraic_geometry.ringed_space
import topology.sheaves.sheaf_condition.basis_le
import topology.sheaves.sheaf_condition.pairwise_intersections

/-!
# Adjunction between `Œì` and `Spec`

-/

noncomputable theory
universe variables u v

namespace algebraic_geometry
open opposite
open category_theory
open structure_sheaf
open prime_spectrum
open topological_space
open algebraic_geometry.LocallyRingedSpace
open Top.presheaf
open Top.presheaf.sheaf_condition

def local_ring.closed_point (R : Type u) [comm_ring R] [local_ring R] :
  prime_spectrum R :=
‚ü®local_ring.maximal_ideal R, (local_ring.maximal_ideal.is_maximal R).is_prime‚ü©
-- move to local_ring
-- to do : maximal ideals are closed points in the prime spectrum of any ring
-- minimal primes are generic points of irreducible components

abbreviation Spec' (R : CommRing) := Spec.to_LocallyRingedSpace.obj (op R)

variable (R : CommRing)

/- basic opens on Spec R -/
def basic_open_B : @Top.opens_index_struct (Spec' R).to_Top := ‚ü®R, Œª r, basic_open r‚ü©
-- lesson: better directly work with the indexing function than the range set!

private def idfo := induced_functor (op ‚àò (basic_open_B R).f)

lemma basic_opens_is_basis : Top.is_basis_range (basic_open_B R) :=
begin
  unfold Top.is_basis_range opens.is_basis basic_open_B,
  convert is_topological_basis_basic_opens,
  rw ‚Üê set.range_comp, dsimp, congr,
end

lemma to_basic_open_epi (r : R) : epi (to_open R (basic_open r)) :=
‚ü®Œª S f g h, by { change f.comp _ = g.comp _ at h,
  rw [‚Üêlocalization_to_basic_open, ‚Üêring_hom.comp_assoc, ‚Üêring_hom.comp_assoc] at h,
  apply (is_iso.epi_of_iso (show CommRing.of _ ‚ü∂ _, from to_basic_open R r)).1,
  apply is_localization.ring_hom_ext _ h,
  swap, exact localization.is_localization }‚ü©

lemma is_localization_iso_comp {M : submonoid R} {S T : CommRing}
  [i : algebra R S] [h : is_localization M S] (f : S ‚âÖ T) :
  @is_localization _ _ M T _ (f.hom.comp i.to_ring_hom).to_algebra :=
{ map_units := let hm := h.1 in
    Œª t, is_unit.map f.hom.to_monoid_hom (hm t),
  surj := let hs := h.2 in Œª t, let ‚ü®‚ü®r,s‚ü©,he‚ü© := hs (f.inv t) in ‚ü®‚ü®r,s‚ü©, by {
    convert congr_arg f.hom he, rw [ring_hom.map_mul, ‚Üêcomp_apply, iso.inv_hom_id], refl}‚ü©,
  eq_iff_exists := let he := h.3 in Œª t t', by { rw ‚Üê he, split,
    apply f.CommRing_iso_to_ring_equiv.injective, exact congr_arg f.hom } }

instance (r : R) : algebra R ((structure_sheaf R).1.obj (op $ basic_open r)) :=
  (to_open R (basic_open r)).to_algebra

/- instance of sections of structure sheaf on basic open as localization of the ring -/
instance (r : R) : is_localization.away r ((structure_sheaf R).1.obj (op $ basic_open r)) :=
by { convert is_localization_iso_comp _ (basic_open_iso R r).symm, /- can't replace _ by R -/
  change ring_hom.to_algebra _ = _, congr' 1,
  exact (localization_to_basic_open R r).symm,
  exact localization.is_localization }

instance (x : prime_spectrum R) : algebra R ((structure_sheaf R).1.stalk x) :=
  (to_stalk R x).to_algebra

instance (x : prime_spectrum R) :
  is_localization.at_prime ((structure_sheaf R).1.stalk x) x.as_ideal :=
by { convert is_localization_iso_comp _ (stalk_iso R x).symm,
  change ring_hom.to_algebra _ = _, congr' 1, erw iso.eq_comp_inv,
  exact to_stalk_comp_stalk_to_fiber_ring_hom R x,
  exact localization.is_localization }

namespace LocallyRingedSpace
variable (X : LocallyRingedSpace)
abbreviation Œì' := Œì.obj (op X)

/- map from the global sections to a stalk -/
def Œì_to_stalk (x : X) : Œì' X ‚ü∂ X.presheaf.stalk x :=
  X.presheaf.germ (‚ü®x,trivial‚ü© : (‚ä§ : opens X))
  -- or @Top.presheaf.germ _ _ _ _ _ ‚ä§ ‚ü®x,trivial‚ü©

/- counit on the underlying set -/
def to_Œì_Spec_fun : X ‚Üí Spec' (Œì' X) :=
Œª x, comap (X.Œì_to_stalk x) (@local_ring.closed_point _ _ (X.local_ring x))

lemma mem_ideal_Œì_to_stalk_iff (r : Œì' X) (x : X) :
  r ‚àâ (X.to_Œì_Spec_fun x).as_ideal ‚Üî is_unit (X.Œì_to_stalk x r) :=
begin
  unfold to_Œì_Spec_fun,
  rw [comap_as_ideal, ideal.mem_comap],
  unfold local_ring.closed_point, rw local_ring.mem_maximal_ideal,
  unfold nonunits, rw [set.mem_set_of_eq, not_not],
end

/- preimage of a basic open under the counit is a basic open -/
lemma to_Œì_Spec_preim_basic_open_eq (r : Œì' X) :
  X.to_Œì_Spec_fun‚Åª¬π' (basic_open r).1
  = (X.to_RingedSpace.basic_open r).1 :=
begin
  ext, dsimp,
  rw [set.mem_preimage, opens.mem_coe,
      mem_basic_open, X.to_RingedSpace.mem_basic_open],
  apply mem_ideal_Œì_to_stalk_iff,
end

/- counit is continuous -/
lemma to_Œì_Spec_continuous : continuous X.to_Œì_Spec_fun :=
begin
  apply is_topological_basis_basic_opens.continuous,
  rintro U ‚ü®r,rfl‚ü©, convert (X.to_RingedSpace.basic_open r).2,
  exact X.to_Œì_Spec_preim_basic_open_eq r,
end

def to_Œì_Spec_Top : continuous_map X (Spec' (Œì' X)) :=
{ to_fun := X.to_Œì_Spec_fun,
  continuous_to_fun := X.to_Œì_Spec_continuous }

lemma to_Œì_Spec_opens_map_obj_basic_open_eq (r : Œì' X) :
  (opens.map X.to_Œì_Spec_Top).obj (basic_open r) = X.to_RingedSpace.basic_open r
  := subtype.eq (X.to_Œì_Spec_preim_basic_open_eq r)

def to_opens_map_basic_open (r : Œì' X) :=
  X.presheaf.map ((opens.map X.to_Œì_Spec_Top).obj (basic_open r)).le_top.op

def is_unit_res_opens_map_basic_open (r : Œì' X) :=
  by { have h := X.to_RingedSpace.is_unit_res_basic_open r,
  rw ‚Üê to_Œì_Spec_opens_map_obj_basic_open_eq at h, exact h }

def to_Œì_Spec_sheaf_app (r : Œì' X) := by {
  let l := is_localization.away.lift r (X.is_unit_res_opens_map_basic_open r),
  swap 5, exact localization.is_localization,
  exact (basic_open_iso _ r).hom ‚â´ l }

/- characterization of the sheaf morphism on basic opens,
   direction ‚Üí used in proving naturality of the morphism,
   direction ‚Üê ... -/
lemma to_Œì_Spec_sheaf_app_prop (r : Œì' X) :
  ‚àÄ f, to_open _ (basic_open r) ‚â´ f = X.to_opens_map_basic_open r
  ‚Üî f = X.to_Œì_Spec_sheaf_app r :=
Œª f, begin
  unfold to_Œì_Spec_sheaf_app, rw ‚Üê iso.inv_comp_eq,
  rw ‚Üê (is_localization.away.away_map.lift_comp r
    (X.is_unit_res_opens_map_basic_open r) : _ = X.to_opens_map_basic_open r),
  swap 5, exact localization.is_localization,
  rw ‚Üê localization_to_basic_open _ r,
  change f.comp _ = _ ‚Üî _, rw ‚Üê ring_hom.comp_assoc,
  split, { intro h, apply is_localization.ring_hom_ext _ h,
  swap, exact localization.is_localization },
  { intro h, rw ‚Üê h, refl },
end

def to_Œì_Spec_sheaf_basic_opens : idfo _ ‚ãô (Spec' (Œì' X)).presheaf
                               ‚ü∂ idfo _ ‚ãô X.to_Œì_Spec_Top _* X.presheaf :=
{ app := X.to_Œì_Spec_sheaf_app,
  naturality' := Œª r s f, by {
    apply (to_basic_open_epi _ r).1,
    simp only [‚Üêcategory.assoc],
    rw (X.to_Œì_Spec_sheaf_app_prop r _).2 rfl,
    convert (X.to_Œì_Spec_sheaf_app_prop s _).2 rfl,
    apply eq.symm, apply X.presheaf.map_comp } }

def to_Œì_Spec_sheaf := sheaf_hom.uniq_extn_from_basis
  ((is_sheaf_iff_is_sheaf_opens_le_cover _).1 (pushforward_sheaf_of_sheaf X.ùí™.2))
  (basic_opens_is_basis _) X.to_Œì_Spec_sheaf_basic_opens

def to_Œì_Spec_SheafedSpace : X.to_SheafedSpace ‚ü∂ (Spec' (Œì' X)).to_SheafedSpace :=
{ base := X.to_Œì_Spec_Top,
  c := X.to_Œì_Spec_sheaf.lift }

lemma to_Œì_Spec_SheafedSpace_app_eq (r : Œì' X) :
  X.to_Œì_Spec_SheafedSpace.c.app (op (basic_open r)) = X.to_Œì_Spec_sheaf_app r :=
by change (whisker_left (idfo _) _).app r = _; erw X.to_Œì_Spec_sheaf.fac; refl

def to_Œì_Spec_SheafedSpace_app_prop (r : Œì' X) := by {
  have h := X.to_Œì_Spec_sheaf_app_prop r,
  rw ‚Üê to_Œì_Spec_SheafedSpace_app_eq at h,
  exact h }

lemma to_stalk_comm (x : X) : to_stalk _ _ ‚â´
  PresheafedSpace.stalk_map X.to_Œì_Spec_SheafedSpace x = X.Œì_to_stalk x :=
begin
  rw PresheafedSpace.stalk_map,
  erw ‚Üê to_open_germ _ (basic_open (1 : Œì' X))
    ‚ü®X.to_Œì_Spec_fun x, by rw basic_open_one; triv‚ü©,
  rw [‚Üêcategory.assoc, category.assoc (to_open _ _)],
  erw stalk_functor_map_germ,
  rw [‚Üêcategory.assoc (to_open _ _), (X.to_Œì_Spec_SheafedSpace_app_prop 1 _).2 rfl],
  unfold Œì_to_stalk, rw ‚Üê stalk_pushforward_germ _ X.to_Œì_Spec_Top X.presheaf ‚ä§,
  congr' 1, change (X.to_Œì_Spec_Top _* X.presheaf).map le_top.hom.op ‚â´ _ = _,
  apply germ_res,
end


def to_Œì_Spec : X ‚ü∂ Spec' (Œì' X) :=
begin
  fsplit, exact X.to_Œì_Spec_SheafedSpace,
  intro x, let p : prime_spectrum (Œì' X) := X.to_Œì_Spec_fun x,
  fsplit, intros t ht,
  have h : is_localization.at_prime ((structure_sheaf (Œì' X)).1.stalk p) p.as_ideal,
  apply_instance,
  have he' := h.surj, rcases he' t with ‚ü®‚ü®r,s‚ü©,he‚ü©,
  have hu := h.map_units,
  let sm := PresheafedSpace.stalk_map X.to_Œì_Spec_SheafedSpace x,
  have hr : is_unit (X.Œì_to_stalk x r),
    apply_fun sm at he,
    rw [‚Üêto_stalk_comm, comp_apply],
    erw ‚Üê he, rw ring_hom.map_mul,
    apply is_unit.mul ht,
    exact is_unit.map sm.to_monoid_hom (hu s),
  rw ‚Üê mem_ideal_Œì_to_stalk_iff at hr,
  have hr' := hu ‚ü®r,hr‚ü©, erw ‚Üê he at hr',
  exact is_unit_of_mul_is_unit_left hr',
end

end LocallyRingedSpace

end algebraic_geometry
